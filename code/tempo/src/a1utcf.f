c      $Id$
      FUNCTION A1UTCF(MJD,FRACT)

C     CALCULATES A1-UTC TIME FROM 1966 VIA LINEAR FORMULAE

      IMPLICIT REAL*8(A-H,O-Z)

C FREQUENCY OFFSET OF UTC FROM A.1 IN UNITS OF 10**-10
C 1966 TO 1971  FREQUENCY OFFSET WAS KEPT AT   -300
C 1972 TO ----  FREQUENCY OFFSET WAS KEPT AT   0
C               LINEAR FORMULAE USED FROM FROM 1.0 JAN 1966 TO
C                  1.0 JAN 1972
C               STEP FUNCTION USED THEREAFTER

C POINTS AT WHICH DISCONTINUITIES OCCURRED

C  INSTANT OF JUMP    JUMP       MJD
C YEAR MONTH DAY HR  (SEC)      
C 1966 DEC   01  00 -0.0007      39460   LINEAR FORMULAE
C 1968 FEB   01  00 -0.100       39887
C 1972 JAN   01  00  0.1076      41317   STEP FUNCTION
C  See leap.sec for subsequent leap second jumps

C DATES OF LEAP SECONDS 
	common/leapsec/mjdleap(50),nleaps

C        LINEAR FORMULA AFTER JAN 1.0 1966 (FREQUENCY OFFSET-300.D-10)
C        AND BEFORE 1.0 JAN 1972 (JULIAN DAY NUMBER 2441318)
      IF(MJD.GE.41318) GO TO 20
      A1UTCF=6.2398697D0+0.002592D0*((MJD-39857)+FRACT)

C     MJD = 39856 = EPOCH OF LINEAR FORMULA
      IF(MJD.GE.39461) GOTO 10
C        CORRECTION BEFORE 1.0 DEC 1966
      A1UTCF=A1UTCF+0.0007D0
      RETURN
   10 IF(MJD.LT.39888) RETURN
C        CORRECTION AFTER 1.0 FEB 1968
      A1UTCF=A1UTCF-0.1D0
      RETURN

C        CONSTANT VALUE AFTER 1.0 JAN 1972 (JUL.DAY NUM. 2441318)
C        WITH ONE SECOND QUANTUM JUMPS
   20 A1UTCF=10.0343817D0
      ILP=0
      DO 30 I=1,NLEAPS
      IF(MJD.LT.MJDLEAP(I)) GOTO 40
   30 ILP=I
   40 A1UTCF=A1UTCF+ILP

      RETURN
      END
