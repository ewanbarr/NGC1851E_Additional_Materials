c      $Id$
      SUBROUTINE OBSITE(pc,prn,rea,tsid,site,sitvel)

C             SIDEREAL TIME TSID ALREADY SET UP: (X,Y,Z) OF
C             OBSERVING SITE RELATIVE TO EARTH CENTER ARE STORED
C             IN REA(I) IN LIGHT SEC. (EPOCH 1950.0)
C	MODIFIED 26 JUL 1984,JMW, TO RETURN OBS VEL W.R.T. GEOCENTER.
C		(PASSED THROUGH COMMON OBSP AS SITVEL(1-3)

      IMPLICIT REAL*8(A-H,O-Z)
      parameter (TWOPI=6.28318530717958648D0)
	real*8 prn(3,3),rea(3),site(3),sitvel(3),eeq(3)

C           COMPUTE LOCAL TRUE SIDEREAL TIME
      PH=TSID+PC-SITE(3)
      EEQ(1)=SITE(1)*DCOS(PH)
      EEQ(2)=SITE(1)*DSIN(PH)
      EEQ(3)=SITE(2)
      DO 20 I=1,3
   20   REA(I)=PRN(I,1)*EEQ(1)+PRN(I,2)*EEQ(2)+PRN(I,3)*EEQ(3)
c     CALCULATE OBSERVATORY VELOCITY W.R.T. GEOCENTER (1950.0):
        SPEED=TWOPI*SITE(1)/(86400.d0/1.00273d0)
	sitera=0.d0
	if(speed.gt.1.d-10) SITERA=DATAN2(REA(2),REA(1))
        SITVEL(1)=-DSIN(SITERA) * SPEED
        SITVEL(2)= DCOS(SITERA) * SPEED
        SITVEL(3)=0.
      RETURN
      END
